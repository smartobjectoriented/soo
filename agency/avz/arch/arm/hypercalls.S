/*
 * Copyright (C) 2014-2018 Daniel Rossier <daniel.rossier@heig-vd.ch>
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 2 as
 * published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 */


#include <config.h>

#include <asm/processor.h>

#include <generated/asm-offsets.h>

.extern hypervisor_stack

.align  5
ENTRY(ret_from_hypercall)

	disable_irq

	vcpu	r8
	ldr		r9, [r8, #(OFFSET_ARCH_VCPU + OFFSET_GUEST_CONTEXT + OFFSET_SYS_REGS + OFFSET_VKSP)]
	str		r0, [r9, #S_R0]

	b	ret_to_user


.align  5
@ Hypercall generated by the kernel guest OS
ENTRY(hypercall_start)

	disable_irq

	sub		sp, sp, #S_FRAME_SIZE

	stmia	        sp, {r0 - r12}

	str		lr, [sp, #S_PC]

	current_cpu r10

	@ Check if we are coming from avz or the guest
	ldr		r8, .LCpseudo_usr_mode

	ldr		r9, [r8, r10, lsl #2]
	cmp		r9, #0
	beq		__do_hypercall_from_avz

	@ readjust stack for future restore
	add		r8, sp, #S_FRAME_SIZE

	str		r8, [sp, #S_SP]

	str		lr, [sp, #S_LR]
	str		r11, [sp, #S_PSR]  		  @ r11 was set by the guest before trampoline

	mov		r12, #0x18		          @ IRQ vector if something happened during hypercall processing
	str		r12, [sp, #S_CONTEXT]

	ldr		r8, .LCpseudo_usr_mode
	mov		r9, #0
	str		r9, [r8, r10, lsl #2]

	mov		r8, sp
	ldr 	r9, .LChypervisor_stack   @ Get the running hypervisor SVC stack
	ldr		sp, [r9, r10, lsl #2]

	vcpu	r9
	str		r8, [r9, #(OFFSET_ARCH_VCPU + OFFSET_GUEST_CONTEXT + OFFSET_SYS_REGS + OFFSET_VKSP)]


process_hypercalls:

	adr	lr, ret_from_hypercall
	adr	tbl, hypercall_table
	add	tbl, tbl, scno, lsl #2
	ldr	pc, [tbl]

@ This is the entry point in case an hypercall is issued from the hypervisor
__do_hypercall_from_avz:

	adr lr, __end_hypercall_from_avz

	adr	tbl, hypercall_table
	add	tbl, tbl, scno, lsl #2

	ldr	pc, [tbl]


@ Fast return path to the caller (in avz)
__end_hypercall_from_avz:

	ldr		lr, [sp, #S_PC]

	ldmia	sp, {r0 - r12}
	add		sp, sp, #S_FRAME_SIZE

	mov		pc, lr

ENTRY(do_arch_sched_op)
	mov		pc, lr


ENTRY(do_dummy)
	mov		pc, lr

ENTRY(do_get_system_time)
	mov		pc, lr


/*
 * hypercall_table
 */
.type hypercall_table, #object
ENTRY(hypercall_table)

__hypercall_start:
	.long do_set_callbacks
	.long do_event_channel_op
	.long do_console_io
	.long do_physdev_op
	.long do_sched_op
	.long do_domctl
	.long do_soo_hypercall
__hypercall_end:

.LCpseudo_usr_mode:
	.word	pseudo_usr_mode
.LChypervisor_stack:
	.word	hypervisor_stack


